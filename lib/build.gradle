apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'org.mozilla.rust-android-gradle.rust-android'
apply plugin: 'maven-publish'
apply plugin: "com.palantir.git-version"
apply plugin: 'pl.allegro.tech.build.axion-release'

def verDetails = versionDetails()

version = "${scmVersion.version}-${verDetails.gitHash}"

android {
    compileSdkVersion 31
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 31
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    publishing {
        singleVariant('release') {
            withSourcesJar()
        }
    }
    ndkVersion rootProject.ext.ndkVersion
    namespace 'land.fx.wnfslib'
}

cargo {
    module  = "../wnfslib"
    libname = "wnfslib"
    targets = ["arm64", "x86", "x86_64", "arm"]
    prebuiltToolchains = true
//    Set to "debug" to enable logging. Or "release" to make a production build.
    profile = "debug"
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'com.github.functionland:fula-build-aar:v0.7.3' // From jitpack.io
}

tasks.whenTaskAdded { task ->
    if ((task.name == 'mergeDebugJniLibFolders' || task.name == 'mergeReleaseJniLibFolders')) {
        task.dependsOn 'cargoBuild'
    }
}

publishing {
    publications {
        release(MavenPublication) {
            groupId = "land.fx"
            artifactId = "wnfs"

            afterEvaluate {
                from components.release
            }
        }
    }
    repositories {
        mavenCentral()
    }
}


repositories {
    mavenCentral()
}
